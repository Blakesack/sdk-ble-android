apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.XYOracleNetwork'

/**
 * If set to true, will build with local versions of the dependencies.
 */
def buildWithLocalProjects = true

ext {
    if (!project.hasProperty('local')) {
        local = buildWithLocalProjects
    }
}

subprojects {
    ext {
        local = this.ext.local
    }
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        google()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$gradleVersion"
    }
}


repositories {
    maven { url 'https://jitpack.io' }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        defaultPublishConfig 'release'
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        htmlReport true
        htmlOutput file("lint-report.html")
        abortOnError true
        warningsAsErrors true
    }

    sourceSets {
        main {
            assets {
                srcDir 'assets'
            }
        }
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    api "androidx.annotation:annotation:$androidxVersion"

    if (local) {
        implementation project(':xycore')
    } else {
        implementation 'com.github.XYOracleNetwork:sdk-core-android:0763fe7c4b'
    }

    implementation 'com.github.kotlin-graphics:kotlin-unsigned:v2.1'
}
